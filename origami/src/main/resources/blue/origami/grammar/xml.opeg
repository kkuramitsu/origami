// Nez
// Nested CDATA Section

File
	= PROLOG? DTD? Xml

Chunk
	= Xml

Expr
	= Xml

PROLOG
	= '<?xml' (!'?>' .)* '?>' S*

DTD
	= '<!' (!'>' .)* '>' S*

Xml
	= { '<' $key(Name) S* $(Attribute)* ( '/>' / '>' S* ($value(Content) / COMMENT)* '</' NAME '>' ) #Element } S*

Name
	= { NAME #Name }

NAME
	= [A-Z_a-z:] ('-' / [.0-9:A-Z_a-z])*

Attribute
	= { $key(Name) S* '=' S* $value(String) #Attr } S*

String
	= '"' { (!'"' .)* #Value } '"'

Content
	= Xml 
	/ CDataSec 
	/ Text

CDataSec
	= '<![CDATA[' { CDATA #CDATA } ']]>' S*
CDATA
	= (!']]>' !'<![CDATA[' .)*
	  ('<![CDATA[' CDATA ']]>' CDATA)?

COMMENT
	= '<!--' (!'-->' .)* '-->' S*

Text
	= { (!'<' .)+ #Text }

S
	= [ \t\r\n]
