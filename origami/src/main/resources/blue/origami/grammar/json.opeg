/**
 * JSON Grammar
 * ECMA-404 The JSON Data Interchange Standard.
 *
 * Reference: 
 *  http://www.ecma-international.org/publications/files/ECMA-ST/ECMA-404.pdf
 *  http://rfc7159.net/rfc7159
 *
 * Authors:
 *  K. Kuramitsu 
 */

/* This is a JSON object: */

example Object&Value '''
{
     "Image": {
         "Width":  800,
         "Height": 600,
         "Title":  "View from 15th Floor",
         "Thumbnail": {
             "Url":    "http://www.example.com/image/481989943",
             "Height": 125,
             "Width":  100
         },
         "Animated" : false,
         "IDs": [116, 943, 234, 38793]
       }
   }
'''

/* This is a JSON array containing two objects: */

example Array&Value '''
[
     {
        "precision": "zip",
        "Latitude":  37.7668,
        "Longitude": -122.3959,
        "Address":   "",
        "City":      "SAN FRANCISCO",
        "State":     "CA",
        "Zip":       "94107",
        "Country":   "US"
     },
     {
        "precision": "zip",
        "Latitude":  37.371991,
        "Longitude": -122.026020,
        "Address":   "",
        "City":      "SUNNYVALE",
        "State":     "CA",
        "Zip":       "94085",
        "Country":   "US"
     }
   ]
'''

/* Here are three small JSON texts containing only values: */

example String&Value "Hello world!" 
example Number&Value 42 
example Value        true
 
File
	= S* Value S* !.


/* Code Layout, Tokens */

S = [ \t\n\r]
"[" = '[' S*
"]" = ']' S*
"{" = '{' S*
"}" = '}' S*
"," = ',' S*
":" = ':' S*

public Expression
	= Value

public Value
	= String
	/ Number
	/ Object
	/ Array
	/ Null
	/ True
	/ False
	/ ObjectId

public Object
	= { "{" ($_(Member) ("," $_(Member) )* )? "}" #Object }

Member
	= { $key(String) ":" $value(Value) #Member }

Key
	= String

ObjectId 
        = 'ObjectId' '("' { ID #ObjectId } '")'

ID
        = [0-9a-z]+

Array
	= { "[" ($(Value) ("," $(Value) )* )? "]" #List }

String
	= '"' { ('\\"' / '\\\\' / !'"' .)* #String } '"' S*

True
        = { 'true'  #True } S*

False
        = { 'false' #False } S*

Null
        = { 'null'  #Null } S*

Number
        = { '-'? INT (FRAC EXP? #Float / '' #Integer) } S*

INT
	= '0' / [1-9] DIGIT*

DIGIT
	= [0-9]

FRAC
	= '.' DIGIT+

EXP
	= [Ee] ('-' / '+')? DIGIT+
	
format #JSON[*]      `{$[0 `,` -1]}`
format #keyvalue[2]  `$[0]:$[1]`
format #List[*]     `[$[0 `,` -1]]`
format #String[0]    `"${text}"`
format #Integer[0]   `${text}`
format #Float[0]     `${text}`
format #True[0]      `true`
format #False[0]     `false`
format #Null[0]      `null`
format #ObjectId[0]  `ObjectId("$[0]")`
