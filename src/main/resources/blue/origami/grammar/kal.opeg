/**
 * KaleidoScope Grammar
 *
 * Authors:
 *   Masanori Sato (https://github.com/masanori-10)
 */

File = Input

example File '''
main 1 + 2
def add(x,y) x + y
'''

Input = { $(Stat)* #Source }

Stat =
  MainFunc
  / FuncDecl
  / ExternFunc

MainFunc = "main" BinopExpr

FuncDecl = {
  "def"
  $name(Identifier)
  "(" $param(Params) ")"
  $body(BinopExpr)
  #FuncDecl
}

ExternFunc = {
  "extern"
  $name(Identifier)
  "(" $param(Params) ")"
  #ExternFunc
}

Params = {
  ( $(Param) ( "," $(Param) )* )?
}

Param = {
  $name(Identifier)
  #LetDecl
}

BinopExpr =
  Expr
  {$left _Binop $right(Expr) }*

_Binop =
  "+" #AddExpr
  / "*" #MulExpr

Expr =
  CallFunc
  / Identifier
  / DoubleLiteral
  / IntegerLiteral

CallFunc = {
  $name(Identifier)
  "(" $param(Arguments) ")"
  #ApplyExpr
}

Arguments = {
  ( $(Expr) ( "," $(Expr) )* )?
}

DoubleLiteral = { DOUBLE #DoubleExpr } _
IntegerLiteral = { INTEGER #IntExpr } _
Identifier = { NAME #NameExpr } _

DOUBLE =
  FRACTION EXPONENT?
  / DECIMAL EXPONENT
INTEGER = DECIMAL
DIGITS = DIGIT+
DIGIT = [0-9]
DECIMAL =
  '0'
  / !'0' DIGITS
FRACTION =
  '.' DIGITS
  / DIGITS '.' DIGITS?
EXPONENT = [eE] [+\-]? DIGITS

NAME = !DIGIT !KEYWORD W+
W = [a-zA-Z0-9_$]
_ = [ \n\r\t\u000B\u000C]*

KEYWORD
  = "def"
  / "extern"
  / "main"

"main" = 'main' !W _
"def" = 'def' !W _
"extern" = 'extern' !W _

"," = ',' _
"(" = '(' _
")" = ')' _
"+" = '+' _
"*" = '*' _
